'#define WOR word';

"---------------------------------------------------------------------------";
"                                                                           ";
"                       Variables en page 0                                 ";
"                                                                           ";
"---------------------------------------------------------------------------";
DEFINE * = 0;
DECLARE PTR_READ_DEST WORD;
DEFINE PTR_READ_DEST_H = PTR_READ_DEST +1;

DECLARE ERRNO;

DECLARE OFFSET_0, OFFSET, OFFSET_H, OFFSET_3;
DEFINE OFFSET_L = OFFSET;

DECLARE TMP;

DEFINE * = $F3;
DECLARE PTR    ;
DECLARE PTR_MAX;


"---------------------------------------------------------------------------";
"                                                                           ";
"                       Variables en page 2                                 ";
"                                                                           ";
"---------------------------------------------------------------------------";
DEFINE * = $27F;
DECLARE TNAME[17];

DEFINE * = $293;
DECLARE TH_NAME[17];
DECLARE TH_DUMMY[4];
DECLARE TH_UNUSED;
DECLARE TH_START WORD;
	DEFINE TH_START_L = TH_START;
	DEFINE TH_START_H = TH_START +1;
DECLARE TH_END WORD;
	DEFINE TH_END_L = TH_END;
	DEFINE TH_END_H = TH_END +1;
DECLARE TH_AUTO;
DECLARE TH_TYPE;
DECLARE TH_STRING_FLAG;
DECLARE TH_INTEGER_FLAG;
DECLARE TH_ERROR;

"---------------------------------------------------------------------------";
"                                                                           ";
"                   Adresses des Routines ROM v1.1                          ";
"                                                                           ";
"---------------------------------------------------------------------------";
DEFINE GetTapeParams = $E7B2;
DEFINE Print_Searching = $E57D;
DEFINE Print_Found = $E594;
DEFINE Print_Loading = $E59B;
DEFINE Print_ErrorFound = $E651;
DEFINE EndTapeLoad = $E8D3;


"---------------------------------------------------------------------------";
"                                                                           ";
"                   Adresses des Buffers ou Tables                          ";
"                                                                           ";
"---------------------------------------------------------------------------";


"---------------------------------------------------------------------------";
"                                                                           ";
"                       En-tête fichier .tap                                ";
"                                                                           ";
"---------------------------------------------------------------------------";
'AUTOEXEC = 0';
'        .db $16, $16, $16, $16, $24, $ff, $ff, $80, AUTOEXEC';
'        .db >end, <end, >($9600-2), <($9600-2), $0B, "CH376-CLOAD", $00';

"---------------------------------------------------------------------------";
"                                                                           ";
"                         Variables et buffers                              ";
"                                                                           ";
"---------------------------------------------------------------------------";
DEFINE * = $9600-2;
DATAW TapeLoad;

DECLARE BUFFER[256];
"'.dsb 256,0';";

"---------------------------------------------------------------------------";
"                                                                           ";
"                        Début du programme                                 ";
"                                                                           ";
"---------------------------------------------------------------------------";


"---------------------------------------------------------------------------";
" TapeLoad:                                                                 ";
"	Simule la commande CLOAD ""                                         ";
"       Charge le fichier sans tenir compte de l'adresse de fin.            ";
"                                                                           ";
" Entree:                                                                   ";
"                                                                           ";
" Sortie:                                                                   ";
"       Retour au BASIC ou exécution si mode AUTO                           ";
"       Sortie sur erreur                                                   ";
"                                                                           ";
"---------------------------------------------------------------------------";
TapeLoad:
	" Appel à la ROM v1.1: Vérification syntaxe CLOAD/CSAVE ";
	" ATTENTION: Faire un HIMEM pour protéger le programme ";
	CALL GetTapeParams;

	CALL OpenTape;

	.A = TH_ERROR; IF ^.Z THEN RETURN;

	CALL Print_Loading;

	" Repositionne au début des données pour pouvoir lire ensuite ";
	" par bloc de 256 octets";
	.A = PTR;
	.Y = 0;
	CALL fseek;

	.A = $FF;
	.Y = .A;
	CALL SetByteRead;
	" IFF ^.Z THEN TapeError; ";

	PTR_READ_DEST <- TH_START;

	REPEAT;
	DO;
		CALL ReadUSBData2;

		.A = PTR_READ_DEST_L;
		IF ^.Z THEN  INC PTR_READ_DEST_H;
		DEC PTR_READ_DEST_L;

		CALL ByteRdGo;
	END;
	UNTIL ^.Z;

	CALL FileClose;

	" Appel à la ROM v1.1: Mise à jour éventuelle des liens BASIC ";
	" Remarque: on n'en revient jamais sauf si mode AUTO -> ";
	"      a remplacer par: UNSTACK .A; UNSTACK .A; GOTO EndTapeLoad; ? ";
	" CALL EndTapeLoad; ";
	GOTO EndTapeLoad;

RETURN;


"---------------------------------------------------------------------------";
" OpenTape:                                                                 ";
"	Ouvre un fichier .tap et charge son entête                          ";
"                                                                           ";
" Entree:                                                                   ";
"                                                                           ";
" Sortie:                                                                   ";
"       TH_ERROR: Code erreur                                               ";
"                                                                           ";
"---------------------------------------------------------------------------";
TapeError:
	TH_ERROR = .A;
	CALL Print_ErrorFound;
	CALL FileClose;
RETURN;

OpenTape:
	CALL InitCH376;
	IFF ^.Z THEN TapeError;

	" TODO: Faire un test sur la longueur du fichier ";

	CALL Print_Searching;
	" Ouverture de l'image tape ";
	.A = #<TNAME;
	.Y = #>TNAME;
	CALL SetFilename;
	CALL FileOpen;
	IFF .A ^= $14 THEN TapeError;

	.A = $FF;
	.Y = .A;
	CALL SetByteRead;
	IFF ^.Z THEN TapeError;

	.A = #<BUFFER;
	.Y = #>BUFFER;
	CALL ReadUSBData;

	" Si le fichier fait moins de 16 caractères -> Erreur ";
	IFF .Y < 16 THEN TapeError;

	.Y = 0;
	.A = BUFFER[.Y]; IFF .A ^= $16 THEN TapeError;

	REPEAT;
	BEGIN;
		INC .Y;
		.A = BUFFER[.Y];
	END;
	UNTIL .A ^= $16;

	IFF .A ^= $24 THEN TapeError;

	" Pour les tableaux ";
	INC .Y; TH_INTEGER_FLAG = BUFFER[.Y];
	INC .Y; TH_STRING_FLAG  = BUFFER[.Y];

	" Type de fichier (Basic, LM, Array)";
	INC .Y; TH_TYPE = BUFFER[.Y];

	" Autostart? ";
	INC .Y; TH_AUTO = BUFFER[.Y];

	" Adresse de fin ";
	INC .Y; TH_END_H = BUFFER[.Y]; INC .Y; TH_END_L = BUFFER[.Y];

	" Adresse de début ";
	INC .Y; TH_START_H = BUFFER[.Y]; INC .Y; TH_START_L = BUFFER[.Y];

	INC .Y; " TH_UNUSED = .A; ";

	.X = $FF;
	REPEAT;
	BEGIN;
		INC .Y;
		INC .X;
		.A = BUFFER[.Y];
		'STA TH_NAME,X';
	END;
	UNTIL .Z;

	TH_ERROR = .A;

	INC .Y;
	PTR = .Y;

	CALL Print_Found;

RETURN;


"---------------------------------------------------------------------------";
"                          Fin du programme                                 ";
"---------------------------------------------------------------------------";

EXIT;

