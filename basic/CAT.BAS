100 '
110 ' AFFICHAGE DU CATALOGUE D'UNE DISQUETTE FTDOS
120 '
130 ' LE PROGRAMME RS DOIT AVOIR ETE PREALABLEMENT CHARGE
140 '
150 HIMEM #95F0:DOKE #2F5, DEEK(#95FE)
       
160 CLS:PAPER 0:INK 7:POKE #26A, PEEK(#26A) AND #FE
       
170 D$ = "FTDOS.DSK"
       
180 POKE #BB80+21,16
       
190 ' LECTURE DU NOM DE LA DISQUETTE
200 P1 = 20:S1 = 1
210 AD = #A000
220 POKE #48D,P1:POKE #48E,S1:DOKE #48F,AD
       
230 POKE #BB80+20,#11:!D$:POKE #BB80+20,#10
       
240 PRINT:PRINT
250 PRINT "   VOLUME : ";
260 FOR I=248 TO 255:PRINT CHR$(PEEK(AD+I));:NEXT
       
270 PRINT:PRINT
       
280 DU = 0:NF = 0
       
290 P1 = 20:S1 = 2
300 REPEAT
310 AD = #A000
       
320 POKE #48D,P1:POKE #48E,S1:DOKE #48F,AD
       
330 POKE #BB80+20,#11:!D$:POKE #BB80+20,#10
       
340 IF ER <> #14 THEN PRINT "Erreur: ";ER:END
       
350 P0 = PEEK(AD):S0 = PEEK(AD+1):P1 = PEEK(AD+2):S1 = PEEK(AD+3)
       
360 AD = AD+4
       
370 FOR I=1 TO 14
380 FP = PEEK(AD):FS = PEEK(AD+1)
       
390 IF FP=255 THEN 460
       
400 PRINT CHR$(PEEK(AD+2));"  ";
       
410 FOR J=1 TO 12:PRINT CHR$(PEEK(AD+2+J));:NEXT
       
420 PRINT "  ";CHR$(PEEK(AD+15));"      ";
       
430 S$=STR$(DEEK(AD+16)):PRINT RIGHT$("   "+S$,3);" SECTORS"
       
440 DU=DU+DEEK(AD+16):NF=NF+1
       
450 REM ->SKIP
460 AD=AD+18
470 ' PAUSE SI UNE TOUCHE EST APPUYEE
480 IF PEEK(#2DF) THEN GET Z$: GET Z$
490 NEXT
500 UNTIL S1=0
       
510 PRINT:PRINT 1394-2-DU"  SECTORS FREE"
       
520 POKE #26A, PEEK(#26A) OR #01
