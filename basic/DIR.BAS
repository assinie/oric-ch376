100 '
110 ' AFFICHAGE DU CATALOGUE D'UNE DISQUETTE SEDORIC
120 '
130 ' LE PROGRAMME RS DOIT AVOIR ETE PREALABLEMENT CHARGE
140 '
150 HIMEM #95F0:DOKE #2F5, DEEK(#95FE)
       
160 CLS:PAPER 0:INK 7:POKE #26A, PEEK(#26A) AND #FE
       
170 D$ = "SEDORIC3.DSK"
       
180 POKE #BB80+21,16
       
190 ' LECTURE DU TYPE DE DISQUETTE
200 P1 = 0:S1 = 2:AD = #A000
210 POKE #48D,P1:POKE #48E,S1:DOKE #48F,AD
       
220 POKE #BB80+20,#11:!D$:POKE #BB80+20,#10
       
230 DT$="???"
240 IF PEEK(AD+22) = 0 THEN DT$="Mst"
250 IF PEEK(AD+22) = 1 THEN DT$="Slv"
       
260 PRINT "Drive A    ("DT$") ";
       
270 ' LECTURE DU NOM DE LA DISQUETTE
280 P1 = 20:S1 = 1:AD = #A000
290 POKE #48D,P1:POKE #48E,S1:DOKE #48F,AD
       
300 POKE #BB80+20,#11:!D$:POKE #BB80+20,#10
       
310 FOR I=9 TO 29:PRINT CHR$(PEEK(AD+I));:NEXT
       
320 PRINT:PRINT
       
330 ' LECTURE DES INFORMATIONS
340 P1 = 20:S1 = 2
350 AD = #A000
       
360 POKE #48D,P1:POKE #48E,S1:DOKE #48F,AD
       
370 POKE #BB80+20,#11:!D$:POKE #BB80+20,#10
       
380 IF ER <> #14 THEN PRINT "Erreur: ";ER:END
390 IF (PEEK(AD)<>#FF) OR (PEEK(AD+1)<>0) THEN PRINT"Disque incorrect":END
       
400 DF = DEEK(AD+2) : ' ESPACE LIBRE
410 NF = DEEK(AD+4) : ' NOMBRE DE FICHIERS
420 NT = PEEK(AD+6) : ' NOMBRE DE PISTES
430 NS = PEEK(AD+7) : ' NOMBRE DE SECTEURS
440 ND = PEEK(AD+8) : ' NOMBRE DE SECTEURS DU CATALOGUE
       
450 ' DENSITE (S/D)
460 DE$ = "S"
470 IF PEEK(AD+9) = NT+#80 THEN DE$="D"
       
480 ' TYPE DE DISQUETTE
490 DT$ = "?"
500 IF PEEK(AD+10) = 0 THEN DT$ = "Master"
510 IF PEEK(AD+10) = 1 THEN DT$ = "Slave"
       
       
520 ' LECTURE DU CATALOGUE
530 P1 = 20:S1 = 4
       
540 OD = 1
550 REPEAT
560 AD = #A000
       
570 POKE #48D,P1:POKE #48E,S1:DOKE #48F,AD
       
580 POKE #BB80+20,#11:!D$:POKE #BB80+20,#10
       
590 IF ER <> #14 THEN PRINT "Erreur: ";ER:END
       
600 P1 = PEEK(AD):S1 = PEEK(AD+1):
610 AD = AD+2
620 AD = AD+14
       
630 FOR I=1 TO 15
       
640 FP = PEEK(AD+12):FS = PEEK(AD+13)
       
650 IF FP+FS = 0 THEN 780
       
       
660 FOR J=0 TO 8:PRINT CHR$(PEEK(AD+J));:NEXT
670 PRINT ".";
680 FOR J=0 TO 2
690 PRINT CHR$(PEEK(AD+9+J));:NEXT
       
700 PRINT " ";
       
       
710 ' SUITE
720 S = PEEK(AD+14)+(PEEK(AD+15) AND #3F)*256
730 S$=STR$(S):PRINT RIGHT$("   "+S$,3);
       
740 ' AFFICHAGE SUR 2 COLONNES
750 OD = OD = 0
760 IF OD THEN PRINT : ELSE PRINT "   ";
       
770 REM ->SKIP
780 AD=AD+16
790 ' PAUSE SI UNE TOUCHE EST APPUYEE
800 IF PEEK(#2DF) THEN GET Z$: GET Z$
810 NEXT
820 UNTIL (P1+S1)=0
       
830 PRINT
       
840 PRINT "***"MID$(STR$(DF),2)" sectors free ";
850 PRINT "("DE$"/"MID$(STR$(NT),2)"/"MID$(STR$(NS),2)") ";
860 PRINT MID$(STR$(NF),2)" Files"
       
870 POKE #26A, PEEK(#26A) OR #01
